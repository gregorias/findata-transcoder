#!/usr/bin/env fish
#
# This script releases a new version of the project.

function get-version --description "Gets the current version of the project from the cabal file."
    cabal run -v0 findata-transcoder version
    or begin
        echo "Could not get the current version. Aborting."
        exit 1
    end
end

set OLD_VERSION (get-version)

echo "Bump up the new version in package.yaml. Once done, confirm [y/n]":
read confirm
switch $confirm
    case y
        echo "$confirm"
    case '*'
        echo "Aborting due to lack of confirmation (user input was $confirm)."
        exit 0
end

echo "Old version: $OLD_VERSION."
and git add package.yaml
and echo "Updating findata-transcoder.cabal…"
and hpack
and git add findata-transcoder.cabal
and set NEW_VERSION (get-version)
and echo "New version: $NEW_VERSION."
and echo "Committing the new version…"
and git commit -m 'chore: release'
and git push
and echo "Tagging the new version ($NEW_VERSION)…"
and git tag v$NEW_VERSION
and git push origin v$NEW_VERSION
